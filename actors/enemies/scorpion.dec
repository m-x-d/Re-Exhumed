actor ExScorpion : ExEnemy 6004
{
	//$Category Monsters
	//$Title Small Scorpion
	
	Radius 10
	Height 12
	Scale 0.3
	
	Mass 20
	Speed 1
	Health 1
	MaxStepHeight 8
	MaxDropOffHeight 8
	
	BloodColor "Green"
	BloodType "ExGreenBloodHit", "ExGreenBloodSplatter"
	
	DeathSound "rat/death"

	+NOTRIGGER
	-COUNTKILL

	States
	{
		Spawn:
		  SCRP A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05, 0.05), ScaleY + frandom(-0.05, 0.05))
			goto Idle
		
		Idle:
			SCRP A 35 A_LookEx(0, 0, 0, 1280)
			loop

		See:
			SCRP AAAAAA 1 A_Wander
			SCRP A 0 A_JumpIf(Random(1, 16) == 5, "Procrastinate")
			SCRP BBBBBB 1 A_Wander
			SCRP B 0 A_JumpIf(Random(1, 16) == 5, "Procrastinate")
			NULL A 0 A_JumpIf(Random(1, 24) == 5, "TryJump")
			loop
			
		TryJump:
			NULL A 0 A_CheckFloor("Jump") //don't jump if mid-air
			goto See

		Jump:
			NULL A 0 ThrustThing(angle*256/360, Random(4, 6), 0, 0)
			NULL A 0 ThrustThingZ(0, Random(30, 40), 0, 1)
			SCRP C 15
			goto See	
			
		Procrastinate:
			"----" "#" 35
			"----" "#" 0 A_JumpIf(Random(1, 2) == 1, "Procrastinate")
			goto Idle

		Death:
			NULL A 0 A_ScreamAndUnblock
			NULL AAAAAAAAAAAAAAA 0 A_SpawnItemEx("ExScorpionChunk", 0, 0, 4, Random(-2, 2), Random(-2, 2), Random(5, 10), Random(0, 256), 0, 100)
			stop
	}
}

Actor ExScorpionChunk : ExAbstractGoreChunk
{
	Radius 8
	Height 8
	Scale 0.3
	
	Mass 50
	
	States
	{
		Fly:
			//NULL A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.1), ScaleY + frandom(-0.1, 0.1))
			NULL A 0 A_Jump(256, "Chunk1", "Chunk2", "Chunk3", "Chunk4")
			stop
			
		Death:
			"----" "#" 0 A_JumpIf(CallACS("ExActivatorIsOnLava") == 1, "LavaDeath")
			"----" "#" 0 A_SpawnItemEx("ExGreenBloodSplash", 0, 0)
			goto Lay
			
		Chunk1:
			SCRG A 3 A_SpawnItemEx("ExGreenBloodParticle", Random(-1, 1), Random(-1, 1))
			loop
			
		Chunk2:
			SCRG B 3 A_SpawnItemEx("ExGreenBloodParticle", Random(-1, 1), Random(-1, 1))
			loop

		Chunk3:
			SCRG C 3 A_SpawnItemEx("ExGreenBloodParticle", Random(-1, 1), Random(-1, 1))
			loop
			
		Chunk4:
			SCRG D 3 A_SpawnItemEx("ExGreenBloodParticle", Random(-1, 1), Random(-1, 1))
			loop
	}
}