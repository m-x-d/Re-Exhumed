actor ExRat : ExEnemy 1806
{
	//$Category Monsters
	//$Title Rat
	
	Radius 10
	Height 12
	Scale 0.5
	
	Mass 20
	Speed 4
	Health 1
	MaxStepHeight 8
	MaxDropOffHeight 32
	
	DeathSound "rat/death"

	+NOTRIGGER
	-COUNTKILL

	States
	{
		Spawn:
		  RAT_ A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.1), ScaleY + frandom(-0.1, 0.1))
			goto Idle
		
		Idle:
			RAT_ A 2 A_LookEx(0, 0, 0, 256)
			loop

		See:
			RAT_ AAAABBBB 1 A_Wander
			NULL A 0 A_JumpIf(Random(1, 8) == 5, "Sqweak")
			NULL A 0 A_JumpIf(Random(1, 12) == 5, "Feed")
			loop

		Sqweak:
			NULL A 0 A_PlaySound("rat/wander", CHAN_AUTO, 0.5, false, ATTN_NORM)
			goto See
			 
		Feed:
			RATF ABABAB 6
			NULL A 0 A_JumpIf(Random(1, 2) == 1, "Feed") //feed some MOAR?
			goto Idle

		Death:
			NULL A 0 A_ScreamAndUnblock
			NULL A 0 A_SpawnItemEx("ExRatHead", 0, 0, 4, Random(-1, 1), Random(-1, 1), Random(3, 4), Random(0, 256))
			NULL A 0 A_SpawnItemEx("ExRatMeat", 0, 0, 4, Random(-1, 1), Random(-1, 1), Random(3, 4), Random(0, 256))
			RATD AAAAABBBBB 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), Random(1, 2))
			RATD C 5
			stop
	}
}

actor ExRatIdle : ExRat 5607
{
	//$Category Monsters
	//$Title Rat (static)
	
	States
	{
		Idle:
			RAT_ A 16
			NULL A 0 A_JumpIf(Random(1, 8) == 5, "Sqweak")
			NULL A 0 A_JumpIf(Random(1, 12) == 5, "Feed") 
			loop
	}
}

actor ExRatHead : ExAbstractGoreChunk
{
	Radius 8
	Height 8
	Scale 0.45
	
	Mass 25
	
	States
	{
		/*Spawn:
			RATG B 1
			goto Fly*/
		
		Fly:
			RATG B 3 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
	}
}

actor ExRatMeat : ExAbstractGoreChunk
{
	Radius 8
	Height 8
	Scale 0.4
	
	Mass 30
	
	States
	{
		/*Spawn:
			RATG A 1
			goto Fly*/
		
		Fly:
			RATG A 3 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
	}
}