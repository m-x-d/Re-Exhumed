actor ExMummy : ExEnemy 1699
{
	//$Category Monsters
	//$Title Mummy
	
	Radius 16
	Height 66
	Scale 0.65

	Speed 1
	Health 67
	MeleeRange 98
	MaxDropOffHeight 96 //256
	MinMissileChance 600 //500 in PT 1.1
	Accuracy 150

	DamageType "ExMummyAttack"
	DamageFactor "ExMummyAttack", 0.0

	SeeSound "mummy/see"
	DeathSound "mummy/death"
	
	+NOTRIGGER
	+NOEXTREMEDEATH
	+NOBLOOD
	+AVOIDMELEE
	+AMBUSH

	States
	{
		Spawn:
			MUMW A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.1), ScaleY + frandom(-0.02, 0.05))
			goto Idle
		
		Idle:
			MUMW A 2 A_LookEx(0, 0, 0, 1024)
			loop
			
		See:
			MUMW AAAABBBBCCCCDDDDEE 1 A_Chase("Melee", "Missile", CHF_RESURRECT)
			NULL A 0 A_PlaySound("mummy/staff_walk", CHAN_AUTO, 0.2, false, ATTN_NORM)
			MUMW EEFFFFBBBB 1 A_Chase("Melee", "Missile", CHF_RESURRECT)
			NULL A 0 A_Jump(60, "Mumble")
			loop
			
		Melee:
			NULL A 0 A_FaceTarget
			MUMA AC 6
			MUMA B 5
			MUMA C 4
			NULL A 0 A_PlaySound("mummy/staff_attack", CHAN_AUTO, 1.1)
			NULL AAAAAAAA 0 A_SpawnItemEx("ExSmokeLowAlpha", Random(20, 50) * RandomPick(-1, 1), Random(20, 50) * RandomPick(-1, 1), 10, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			NULL A 0 A_QuakeEx(1, 1, 3, 16, 0, 194, "", QF_SCALEDOWN | QF_WAVE)
			MUMA D 4 A_Explode(55, 110, XF_NOTMISSILE, true)
			NULL A 0 A_RadiusThrust(800, 110, RTF_NOTMISSILE)
			MUMA EFG 8
			goto See
			
		Heal:
		  MUMA AC 6
			MUMA B 5
			MUMA C 4 A_PlaySound("mummy/staff_attack", CHAN_AUTO, 1.1)
			NULL AAAAAAAA 0 A_SpawnItemEx("ExSmokeLowAlpha", Random(20, 50) * RandomPick(-1, 1), Random(20, 50) * RandomPick(-1, 1), 10, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			MUMA D 4
			MUMA EFG 8
			goto See	
			
		Missile:
			NULL A 0 A_FaceTarget
			MUMA AC 6
			MUMA B 5
			MUMA C 4
			NULL A 0 A_PlaySound("mummy/staff_attack", CHAN_AUTO, 0.7)
			NULL AAAAAAAA 0 A_SpawnItemEx("ExSmokeLowAlpha", Random(20, 50) * RandomPick(-1, 1), Random(20, 50) * RandomPick(-1, 1), 10, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			NULL A 0 A_QuakeEx(1, 1, 3, 16, 0, 400, "", QF_SCALEDOWN | QF_WAVE)
			NULL A 0 A_Explode(55, 110, XF_NOTMISSILE, true)
			NULL A 0 A_RadiusThrust(800, 110, RTF_NOTMISSILE)
			MUMA D 4 A_Jump(48, "MissileMorphing")
			goto MissileRegular

		MissileRegular:
			MUMA D 4 A_CustomMissile("ExMummyMissile", 70, 0)
			MUMA EFG 8
			goto See
		
		MissileMorphing:
			MUMA D 4 A_CustomMissile("ExMummyMorphingMissile", 70, 0)
			MUMA EFG 8
			goto See
			
		Mumble:
			NULL A 0 A_PlaySound("mummy/chant", CHAN_AUTO, 0.3, false, 7)
			goto See
			
		Death:
			MUMD A 4 A_ScreamAndUnblock
			NULL A 0 A_GiveToTarget("ExScoreMarker", 400)
			MUMD BC 4
			NULL A 0 A_PlaySound("mummy/unwrap", CHAN_AUTO, 0.3)
			MUMD DEFG 4
			NULL A 0 A_PlaySound("mummy/staff_fall", CHAN_AUTO, 0.3)
			MUMD H 4
			NULL A 0 A_SpawnItemEx("ExMana")
			MUMD I -1
			stop
			
		Raise:
			MUMD I 20 //wait for the staff hit...
		  MUMD H 4 A_PlaySound("mummy/crack", CHAN_AUTO, 0.8)
			MUMD GFEDCBA 4
			goto See	
	}
}

actor ExMummyMissileSmoke : ExSmoke
{
	RenderStyle Add
	Scale 0.4
	Alpha 0.2
}

actor ExMummyMorphingMissileSmoke : ExMummyMissileSmoke
{
	RenderStyle AddStencil
	StencilColor "f4 57 57"
}

actor ExMummyMissile
{
	Height 8
	Radius 6
	Damage (random(15, 20)) //5 in PT 1.1
	Speed 8 //4 in PT 1.1
	Renderstyle Add
	Alpha 1.0
	Scale 0.5

	SeeSound "mummy/poj_spawn"
	DeathSound "mummy/poj_hit"

	Projectile

	+SEEKERMISSILE
	+RANDOMIZE
	+FORCEXYBILLBOARD
	
	States
	{
		Spawn:
			MPRJ AB 5
			NULL A 0 A_PlaySound("mummy/poj_fly", CHAN_AUTO, 0.5, true)
			goto Fly
			
		Fly:
			NULL A 0 A_ScaleVelocity(1.08) //1.03 in PT 1.1
			MPRJ CCCCDDDD 1 A_SpawnItemEx("ExMummyMissileSmoke", 0, 0, 0, velx * 0.1, vely * 0.1, velz * 0.1 + FRandom(0.1, 0.5), angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
			NULL A 0 A_SeekerMissile(10, 20, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
			NULL A 0 A_ScaleVelocity(1.08) //1.03 in PT 1.1
			MPRJ EEEEDDDD 1 A_SpawnItemEx("ExMummyMissileSmoke", 0, 0, 0, velx * 0.1, vely * 0.1, velz * 0.1 + FRandom(0.1, 0.5), angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
			NULL A 0 A_SeekerMissile(10, 20, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
			loop
			
		Death:
			MPRD ABCD 6
			stop
 }
}

actor ExMummyMorphingMissile
{
	Height 16
	Radius 8
	Damage 1
	Speed 7 //4 in PT 1.1
	Renderstyle Add
	Alpha 1.0
	Scale 0.3

	SeeSound "mummy/poj_spawn"
	DeathSound "mummy/poj_hit"

	Projectile

	+SEEKERMISSILE
	+RANDOMIZE
	+FORCEXYBILLBOARD

	States
	{
		Spawn:
			MMPR AB 5
			NULL A 0 A_PlaySound("mummy/poj_fly", CHAN_AUTO, 0.5, true)
			goto Fly

		Fly:
			NULL A 0 A_ScaleVelocity(1.03)
			MMPR CCCCDDDD 1 A_SpawnItemEx("ExMummyMorphingMissileSmoke", 0, 0, 0, velx * 0.1, vely * 0.1, velz * 0.1 + FRandom(0.1, 0.5), angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
			NULL A 0 A_SeekerMissile(10, 25, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
			NULL A 0 A_ScaleVelocity(1.03)
			MMPR EEEEDDDD 1 A_SpawnItemEx("ExMummyMorphingMissileSmoke", 0, 0, 0, velx * 0.1, vely * 0.1, velz * 0.1 + FRandom(0.1, 0.5), angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM) 
			NULL A 0 A_SeekerMissile(10, 25, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
			loop
			
		Death:
			NULL A 0 ACS_NamedExecute("ExOnMorphMissileHit", 0, x, y, z + floorz)
			MMPD ABCD 6
			stop
	 }
}

actor ExNoFlash
{
	RenderStyle None

	States
	{
		Spawn:
			NULL A 1
			stop
	 }
}