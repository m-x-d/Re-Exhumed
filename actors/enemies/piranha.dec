actor ExPiranha : ExEnemy 1913
{
	//$Category Monsters
	//$Title Angry Piranha

	Radius 16
	Height 48
	XScale 0.6
	YScale 0.55
	Speed 4
	Health 32 //2 machete hits
	MeleeRange 42
	PainChance 196
	MaxStepHeight 0
	Accuracy 50

	SeeSound "piranha/see"

	+FLOAT
	+NOGRAVITY
	
	States
	{
		Spawn:
			PRNM A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.15), ScaleY + frandom(-0.1, 0.1))
			goto Idle
		
		Idle:
			PRNM A 5 ThrustThing(angle*256/360 + 64, 2, 0, 0)
			PRNM B 2 A_LookEx(0, 0, 0, 256, 360)
			PRNM C 5 ThrustThing(angle*256/360 + 192, 2, 0, 0)
			PRNM B 2 A_LookEx(0, 0, 0, 256, 360)
			loop	
	
		See:
			PRNM A 0 ThrustThing(angle*256/360 + 64 , 2, 0, 0)
			PRNM AAAAABB 1 A_Chase
			NULL A 0 A_JumpIf(waterlevel != 3, "PushDown")
			NULL A 0 ThrustThing(angle*256/360 + 192 , 2, 0, 0)
			PRNM CCCCC 1 A_Chase
			NULL A 0 A_JumpIf(waterlevel != 3, "PushDown")
			loop
			
		Wander:
			PRNM A 5 ThrustThing(angle*256/360 + 64, 2, 0, 0)
			PRNM B 2 A_Wander
			NULL A 0 A_JumpIf(waterlevel != 3, "PushDown")
			PRNM C 5 ThrustThing(angle*256/360 + 192, 2, 0, 0)
			PRNM B 2 A_Wander
			NULL A 0 A_JumpIf(waterlevel != 3, "PushDown")
			NULL A 0 A_Jump(64, "StopWandering")
			loop
			
		StopWandering:
			NULL A 0 A_ChangeFlag("FRIGHTENED", 0)
			goto Idle	

		PushDown:
			NULL A 0 A_JumpIf(!CallACS("ExTargetIsUnderwater", tidtohate), 2) //skip next line if a target is underwater
			NULL A 0 A_JumpIfTargetInLOS("JumpAttack", 0, 0, 512, 128)
			NULL A 0 A_ChangeVelocity(0, 0, -4)
			goto See
			
		JumpAttack:
			NULL A 0 A_ChangeFlag("COUNTKILL", 0)
			NULL A 0 A_FaceTarget
			NULL A 0 A_PlaySound("piranha/jumpsplash", CHAN_AUTO, 0.75)
			NULL A 0 A_ThrowGrenade("ExJumpingPiranha", 0, 10, 16)
			stop	

		Melee:
			NULL A 0 A_FaceTarget
			NULL A 0 A_JumpIf(CallACS("ExGetPlayerZ") > z, "UpperAttack")
			NULL A 0 A_Jump(128, "Melee2")
			PRA1 AB 6 A_CustomMeleeAttack(8)
			NULL A 0 A_PlaySound("piranha/attack", CHAN_AUTO, 0.75)
			goto See
			
		Melee2:
			PRA2 A 6
			PRA2 B 5 A_CustomMeleeAttack(8)
			NULL A 0 A_PlaySound("piranha/attack", CHAN_AUTO, 0.75)
			goto See
			
		Pain:
			NULL A 0 A_ChangeFlag("FRIGHTENED", 1)
			goto Wander
			
		UpperAttack:
			PRAU A 6
			PRAU B 5 A_CustomMeleeAttack(8)
			NULL A 0 A_PlaySound("piranha/attack", CHAN_AUTO, 0.75)
			goto See
			
		Death:
			NULL A 0 A_Jump(30, "XDeath") //because it's kinda hard to cause XDeath with a machete :)
			NULL A 0 A_Jump(64, "Death2")
			PIRD A 8 A_NoBlocking
			NULL A 0 A_PlaySound("piranha/death", CHAN_AUTO, 1.0)
			NULL A 0 A_CheckFloor("DeathOnFloor")
			NULL A 0 A_SpawnItemEx("ExPiranhaCorpse")
			stop
			
		DeathOnFloor:
			PIRD BBBBCCCCDDDDEEEEFFFF 2 A_SpawnItemEx("ExUnderwaterBloodBubble", Random(-16, 16), Random(-16, 16), Random(16, 32))
			goto Lay
			
		Lay:
			PIRD F 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		Death2:
			PIRX A 8 A_NoBlocking
			NULL A 0 A_PlaySound("piranha/death", CHAN_AUTO | CHAN_UI, 1.0)
			NULL A 0 A_CheckFloor("Death2OnFloor")
			NULL A 0 A_SpawnItemEx("ExPiranhaCorpse2")
			stop

		Death2OnFloor:
			PRD2 BBBBCCCCDDDD 2 A_SpawnItemEx("ExUnderwaterBloodBubble", Random(-16, 16), Random(-16, 16), Random(16, 32))
			goto Lay2
			
		Lay2:
			PRD2 D 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		XDeath:
			PIRX A 6 A_NoBlocking
			NULL A 0 A_PlaySound("piranha/death", CHAN_AUTO | CHAN_UI, 1.0)
			PIRX B 6
			NULL AAAAAAAAAAAAA 0 A_SpawnItemEx("ExUnderwaterBloodBubble", Random(-24, 24), Random(-24, 24), Random(0, 32), 0, 0, 32)
			NULL A 0 A_SpawnItemEx("ExPiranhaGore1", 0, 0, 16, velx + Random(-2, 2), vely + Random(-2, 2), Random(1, 3), Random(0, 256), 0, 0)
			NULL A 0 A_SpawnItemEx("ExPiranhaGore2", 0, 0, 16, velx + Random(-2, 2), vely + Random(-2, 2), Random(1, 3), Random(0, 256), 0, 0)
			NULL A 0 A_SpawnItemEx("ExPiranhaGore3", 0, 0, 16, velx + Random(-2, 2), vely + Random(-2, 2), Random(1, 3), Random(0, 256), 0, 0)
			PIRX C 6
			NULL A 0 A_SpawnItemEx("ExUnderwaterBloodCloud", 0, 0, 16)
			PIRX DE 6
			stop
			
		Vanish:
			NULL A 0
			stop
	}
}

//Behold! Piranjectile!
actor ExJumpingPiranha
{
  Radius 8
	Height 4
	XScale 0.6
	YScale 0.55
	Mass 35
	Damage (random(35, 50))
	Health 1
	Speed 14
	
	DeathSound "gore/splat"

	Projectile
	
	-NOGRAVITY
	+SEEKERMISSILE
	+SHOOTABLE
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+BLOODSPLATTER
	+STEPMISSILE
	+NOTRIGGER

	States
	{
		Spawn:
			PRA1 A 0 NoDelay A_JumpIf(waterlevel == 3, "SpawnPiranha")
			PRA1 A 0 A_ScaleVelocity(0.8)
			PRA1 A 0 A_SetScale(frandompick(-ScaleX, ScaleX), ScaleY)
			PRA1 A 0 A_PlaySound("piranha/attack2", CHAN_AUTO, 0.75)
			PRA1 A 0 A_JumpIf(velz > 0, "UpperAttack")
			PRA1 AB 6 A_CustomMeleeAttack(random(35, 50))
			loop
			
		UpperAttack:
		  PRAU AB 6 A_CustomMeleeAttack(random(35, 50))
			goto Spawn
			
		SpawnPiranha:
			NULL A 0 A_SpawnItemEx("ExPiranha")
			stop	
			
		Death:
			NULL A 0 A_SetScale(frandompick(-ScaleX, ScaleX), ScaleY)
			PIRX AAAA 1     A_SpawnItemEx("ExBloodParticle", Random(-16, 16), Random(-16, 16), Random(2, 4), Random(2, 4) * RandomPick(-1, 1), Random(2, 4) * RandomPick(-1, 1), Random(6, 8))
			PRD2 BBBBCCCC 1 A_SpawnItemEx("ExBloodParticle", Random(-16, 16), Random(-16, 16), Random(2, 4), Random(2, 4) * RandomPick(-1, 1), Random(2, 4) * RandomPick(-1, 1), Random(6, 8))
			goto Lay
			
		Lay:
			PRD2 D 250
			NULL A 0 A_CheckSight("Vanish")
			loop
			
		Vanish:
			NULL A 0
			stop			
	}
}

//GORE
actor ExPiranhaGore1 : ExAbstractGoreChunk
{
	Gravity 30

	States
	{
		Fly:
			PGOR ABCD 8 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", Random(-2, 2), Random(-2, 2), Random(-2, 2))
			goto FlyLoop
			
		FlyLoop:
			PGOR A 8 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", Random(-2, 2), Random(-2, 2), Random(-2, 2))
			loop
			
		Death:
			PGOR A 0 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", 0, 0, 16)
			goto Lay
	}
}

actor ExPiranhaGore2 : ExAbstractGoreChunk
{
	Gravity 20

	States
	{
		Fly:
			PGOR FGHI 8 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", Random(-2, 2), Random(-2, 2), Random(-2, 2))
			goto FlyLoop
			
		FlyLoop:
			PGOR F 8 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", Random(-2, 2), Random(-2, 2), Random(-2, 2))
			loop
			
		Death:
			PGOR F 0 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", 0, 0, 16)
			goto Lay
	}
}

actor ExPiranhaGore3 : ExAbstractGoreChunk
{
	Gravity 25
	//DeathSound "gore/splat"

	States
	{
		Fly:
			PGOR KLMN 8 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", Random(-2, 2), Random(-2, 2), Random(-2, 2))
			goto FlyLoop
			
		FlyLoop:
			PGOR K 8 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", Random(-2, 2), Random(-2, 2), Random(-2, 2))
			loop
			
		Death:
			PGOR K 0 A_SpawnItemEx("ExUnderwaterBloodCloudSmall", 0, 0, 16)
			goto Lay
	}
}

actor ExPiranhaCorpse : ExAbstractGoreChunk
{
	Gravity 20.0
	Mass 500

	States
	{
		Fly:
			PIRD BBBBBBCCCCCC 1 A_SpawnItemEx("ExUnderwaterBloodBubble", Random(-16, 16), Random(-16, 16), Random(16, 32))
			loop
			
		Death:
			NULL A 0 A_SpawnItemEx("ExUnderwaterBloodCloud", 0, 0, 16)
			PIRD DEF 8
			goto Lay
	}
}

actor ExPiranhaCorpse2 : ExAbstractGoreChunk
{
	Gravity 20.0
	Mass 500

	States
	{
		Fly:
			PRD2 BBBBBBCCCCCC 1 A_SpawnItemEx("ExUnderwaterBloodBubble", Random(-16, 16), Random(-16, 16), Random(16, 32))
			loop
			
		Death:
			PRD2 D 0 A_SpawnItemEx("ExUnderwaterBloodCloud", 0, 0, 16)
			goto Lay
	}
}