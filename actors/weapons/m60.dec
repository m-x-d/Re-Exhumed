actor ExM60 : ExWeapon 490 
{
	//$Category Weapons
	//$Title M60
	
	Weapon.Ammotype "ExM60Bullets"
	Weapon.Ammouse 1
	Weapon.Ammogive 24
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 1
	Weapon.SisterWeapon ExM60Powered
	Scale 0.4
	AttackSound "m60/fire"
	Inventory.Pickupmessage "M60"
	
	States 
	{
		Spawn:
			WM60 A -1
			stop

		Ready:
			W60_ A 0 A_JumpIf(args[1] == 66, "Ready6")
			W60_ A 0 A_JumpIf(args[1] == 69, "Ready3")
			W60_ A 0 A_JumpIfInventory("ExM60Bullets", 9, "Ready99")
			W60_ A 0 A_JumpIf(CallACS("ExGetM60AmmoCount") == 6, "Ready6")
			W60_ A 0 A_JumpIf(CallACS("ExGetM60AmmoCount") == 3, "Ready3")
			W60_ A 0 A_JumpIf(CallACS("ExGetM60AmmoCount") == 0, "Ready0")
			W60_ A 1 A_WeaponReady(WRF_ALLOWRELOAD) //failsafe
			loop

		Ready99:
			W60_ A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop

		Ready6: //6 bullets left
			W60F P 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop

		Ready3: //3 bullets left
			W60F T 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
			
		Ready0: //no bullets left...
			W60F X 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop	

		Select:
			W60_ A 0 A_SetCrosshair(8)
			W60S ABCDEFGHIJKLMNOPQRSTUV 1 Offset(0, 32)
			W60_ A 0 A_Raise
			wait

		Deselect:
			NULL A 0 A_JumpIf(CallACS("ExGetM60AmmoCount") == 0, "DeselectEmpty")
			W60D ABCDEFGHIJK 1
			W60_ A 0 A_Lower
			wait
			
		DeselectEmpty:
			NULL A 0 A_JumpIf(!CallACS("ExFireButtonPressed"), 3)
			NULL A 0 A_PlayWeaponSound("m60/click")
			W60F YZX 1 //fake fire
			W60E ABCDEFGH 1
			W60D IJK 1
			W60_ A 0 A_Lower
			wait	

		Fire:
			NULL A 0 A_SetArg(0, 0) //used to fire 3 bullets
			NULL A 0 A_SetArg(2, CallACS("ExGetM60AmmoCount"))
			NULL A 0 A_JumpIf(args[2] == 9, "Fire9Left")
			NULL A 0 A_JumpIf(args[2] == 6, "Fire6Left")
			NULL A 0 A_JumpIf(args[2] == 3, "Fire3Left")
			NULL A 0 A_JumpIf(args[1] == 63, "Fire9Left")
			NULL A 0 A_JumpIf(args[1] == 66, "Fire6Left")
			NULL A 0 A_JumpIf(args[1] == 69, "Fire3Left")
			NULL A 0 A_Jump(170, "Fire1", "Fire2")
			goto Fire3

		Fire1:
			W60F A 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F B 1 A_SetPitch(pitch - 0.1)
			W60F C 1
			W60F D 1 A_SetPitch(pitch + 0.05)
			W60F D 1 A_SetTics(RandomPick(1, 1, 2))
			NULL A 0 A_SetArg(0, args[0] + 1)
			NULL A 0 A_SetArg(1, args[1] + 1)
			NULL A 0 A_JumpIf(args[0] == 3, "DoRefire")
			NULL A 0 A_Jump(128, "Fire2")
			goto Fire3

		Fire2:
			W60F E 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F F 1 A_SetPitch(pitch - 0.1)
			W60F G 1
			W60F H 1 A_SetPitch(pitch + 0.05)
			W60F H 1 A_SetTics(RandomPick(1, 1, 2))
			NULL A 0 A_SetArg(0, args[0] + 1)
			NULL A 0 A_SetArg(1, args[1] + 1)
			NULL A 0 A_JumpIf(args[0] == 3, "DoRefire")
			NULL A 0 A_Jump(128, "Fire1")
			goto Fire3
		
		Fire3:
			W60F I 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F J 1 A_SetPitch(pitch - 0.1)
			W60F K 1
			W60F L 1 A_SetPitch(pitch + 0.05)
			W60F L 1 A_SetTics(RandomPick(1, 1, 2))
			NULL A 0 A_SetArg(0, args[0] + 1)
			NULL A 0 A_SetArg(1, args[1] + 1)
			NULL A 0 A_JumpIf(args[0] == 3, "DoRefire")
			NULL A 0 A_Jump(128, "Fire2")
			goto Fire1

		//when 9 bullets left  
		Fire9Left:
			W60F A 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F B 1 A_SetPitch(pitch - 0.1)
			W60F C 1 A_SetPitch(pitch + 0.05)
			W60F D 1 A_SetTics(RandomPick(2, 2, 3))
			
			W60F E 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F F 1 A_SetPitch(pitch - 0.1)
			W60F G 1 A_SetPitch(pitch + 0.05)
			W60F H 1 A_SetTics(RandomPick(2, 2, 3))
			
			W60F M 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F N 1 A_SetPitch(pitch - 0.1)
			W60F O 1 A_SetPitch(pitch + 0.05)
			W60F P 1 A_SetTics(RandomPick(1, 1, 2))
			NULL A 0 A_SetArg(1, args[1] + 3)
			goto DoRefire

		//when 6 bullets left
		Fire6Left:
			W60F Q 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F R 1 A_SetPitch(pitch - 0.1)
			W60F S 1 A_SetPitch(pitch + 0.05)
			W60F P 1 A_SetTics(RandomPick(2, 2, 3))
			
			W60F Q 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F R 1 A_SetPitch(pitch - 0.1)
			W60F S 1 A_SetPitch(pitch + 0.05)
			W60F P 1 A_SetTics(RandomPick(2, 2, 3))
			
			W60F Q 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F R 1 A_SetPitch(pitch - 0.1)
			W60F S 1 A_SetPitch(pitch + 0.05)
			W60F T 1 A_SetTics(RandomPick(1, 1, 2))
			NULL A 0 A_SetArg(1, args[1] + 3)
			goto DoRefire

		//when 3 bullets left
		Fire3Left:
			W60F U 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F V 1 A_SetPitch(pitch - 0.1)
			W60F W 1 A_SetPitch(pitch + 0.05)
			W60F T 1 A_SetTics(RandomPick(2, 2, 3))
			
			W60F U 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F V 1 A_SetPitch(pitch - 0.1)
			W60F W 1 A_SetPitch(pitch + 0.05)
			W60F T 1 A_SetTics(RandomPick(2, 2, 3))
			
			W60F U 1 A_FireBullets(1, 1, 1, 6, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_Recoil(0.1 * cos(pitch))
			NULL A 0 A_SetAngle(angle + frandom(-0.25, 0.25))
			NULL A 0 A_CustomMissile("ExM60CaseSpawner", 32, 0, 0, CMF_AIMDIRECTION, -pitch)
			W60F V 1 A_SetPitch(pitch - 0.1)
			W60F W 1 A_SetPitch(pitch + 0.05)
			W60F X 1 A_SetTics(RandomPick(1, 1, 2))
			NULL A 0 A_SetArg(1, args[1] + 3)
			goto DoRefire

		DoRefire:
			NULL A 0 A_JumpIf(args[1] == 72, "Reload") //reload after 72 bullets
			//NULL A 0 A_Refire
			goto Ready

		Reload:
			NULL A 0 A_JumpIfNoAmmo("Ready")
			NULL A 0 A_JumpIf(args[1] == 0, "Ready") //no need to reload
			NULL A 0 A_SetArg(1, 0) //reset clip counter
			NULL A 0 A_PlayWeaponSound("m60/reload")
			W60R ABCDEFGHIJKLMNOPQR 1
			W60R S 18
			W60R T 8
			W60R U 4
			W60R VWXY 1
			W60R Z 9
			W61R ABCDEFGH 1
			W61R I 3
			W61R JKLMNO 1
			W61R P 3
			W61R QSTUVWXYZ 1
			W62R ABCDEFGHI 1
			goto Ready
	}
}

actor ExM60CaseSpawner 
{
	Speed 25

	Projectile

	+NOCLIP
	+CLIENTSIDEONLY

	States 
	{
		Spawn:
			NULL A 1 NoDelay A_CustomMissile("ExM60BulletCase", 16, 30, random(-80, -100), CMF_AIMDIRECTION, random(40, 80))
			stop
	}
}

actor ExM60BulletCase 
{
	Height 4
	Radius 4

	Speed 5
	Scale 0.25
	BounceType "Doom"
	BounceFactor 0.5
	BounceCount 4
	MaxStepHeight 4
	ReactionTime 15
	Mass 1

	Projectile
	
	-NOGRAVITY
	+BOUNCEONACTORS
	+ALLOWBOUNCEONACTORS
	+DONTSPLASH
	+BOUNCEAUTOOFF
	+BOUNCEONWALLS
	+MOVEWITHSECTOR
	+FORCEXYBILLBOARD

	BounceSound "m60/casing"

	States 
	{
		Spawn:
			CASE ABCDEFGH 3 A_Countdown
			NULL A 0 A_ScaleVelocity(0.85)
			loop

		Death:
			"####" "A" 0 A_Jump(256, "Rest1", "Rest2", "Rest3", "Rest4","Rest5")
			Goto Rest1

		Rest1:
			"####" "I" 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		Rest2:
			"####" "J" 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		Rest3:
			"####" "K" 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		Rest4:
			"####" "L" 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		Rest5:
			"####" "M" 250
			NULL A 0 A_CheckSight("Vanish")
			loop

		Vanish:
			NULL A 0
			Stop
	}
}