//$gzdb_skip
actor ExMagnumPowered : ExWeapon 
{
	Weapon.Ammotype "ExMagnumClip"
	Weapon.Ammouse 1
	Weapon.Ammogive 0
	Weapon.SisterWeapon ExMagnum
	AttackSound "magnum/fire_powered"

	+BLOODSPLATTER
	+EXTREMEDEATH
	+POWERED_UP

	States 
	{
		Ready:
			PAG_ A 0 A_SetCrosshair(8)
			PAG_ A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto Ready1
			
		Ready1:
			PAGO A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			NULL A 0 A_Jump(128, "Ready3")
			goto Ready2
			
		Ready2:
			PAGO B 1 A_WeaponReady(WRF_ALLOWRELOAD)
			NULL A 0 A_Jump(128, "Ready4")
			goto Ready3
			
		Ready3:
			PAGO C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			NULL A 0 A_Jump(128, "Ready1")
			goto Ready4
			
		Ready4:
			PAGO D 1 A_WeaponReady(WRF_ALLOWRELOAD)
			NULL A 0 A_Jump(128, "Ready2")
			goto Ready1				

		Select:
			PAG_ A 0 A_SetCrosshair(8)
			NULL A 0 A_ChangeFlag("FRIENDLY", 0) //cause we aint have no working uservars on weapons... 
			PAGR EDCBA 1 Offset(0, 32)
			PAG_ A 0 A_Raise
    	wait

		Deselect:
			NULL A 0 A_CheckFlag("FRIENDLY", 6) //cause we aint have no working uservars on weapons... 
			NULL A 0 A_JumpIf(!CallACS("ExFireButtonPressed"), 5)
			NULL A 0 A_PlayWeaponSound("magnum/click")
			NULL A 0 A_ChangeFlag("FRIENDLY", 1) //cause we aint have no working uservars on weapons...
			PAG_ CD 1
			PAGD ABCD 1
			PAG_ A 0 A_Lower
			wait 

		Fire: //regular: 12 frames
			NULL A 0 A_JumpIfNoAmmo("Ready")
			NULL A 0 A_FireBullets(0, 0, 1, 10, "ExBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			NULL A 0 A_QuakeEx(1, 1, 1, 6, 0, 450, "", QF_SCALEDOWN | QF_WAVE)
			NULL A 0 A_SetAngle(angle + frandom(-0.5, 0.5))
			PAG_ B 1 A_SetPitch(pitch - 0.15)
			PAG_ C 1 A_SetPitch(pitch - 0.25)
			PAG_ D 1 A_SetPitch(pitch - 0.4)
			PAG_ E 1 A_SetPitch(pitch + 0.2)
			PAG_ FG 1
			PAG_ A 1
			PAG_ A 0 A_JumpIf(args[0] == 5, "Reload")
			PAG_ A 0 A_SetArg(0, args[0] + 1)
			goto Ready

		Reload:
			NULL A 0 A_JumpIfNoAmmo("Ready")
			NULL A 0 A_JumpIf(args[0] == 0, "Ready") //no need to reload
			NULL A 0 A_SetArg(1, 0)
			PAGR B 1
			PAGR C 1 A_PlayWeaponSound("magnum/reload")
			PAGR DE 1
			goto SpawnCases

		SpawnCases:
			NULL A 0 A_JumpIf(args[1] == args[0], "ReloadFinish")
			NULL A 0 A_SetArg(1, args[1] + 1)
			NULL A 1 A_CustomMissile("ExMagnumPoweredBulletCase", 30, 0, random(100, -100), CMF_AIMDIRECTION, random(-40, -80))
			loop

		ReloadFinish:
			NULL A 1 A_SetArg(0, 0)
			PAGR ED 1
			PAGR C 1 A_PlayWeaponSound("magnum/reload")
			PAGR B 1
			goto Ready
	}
}

actor ExMagnumPoweredBulletCase : ExMagnumBulletCase
{
	States 
	{
		Spawn:
			PCAS ABCDEFGH 3 A_Countdown
			NULL A 0 A_ScaleVelocity(0.85)
			loop
	}
}