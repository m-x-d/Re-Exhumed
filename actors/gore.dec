//BLOOD - GORE

//BLOOD
actor ExBloodSplash 
{
	+NOBLOCKMAP
	+NOGRAVITY

	States 
	{
		Spawn:
			NULL A 2
			NULL AAAAAAAAAAA 0 A_SpawnItemEx("ExBloodDrop", Random(-1, 1), Random(-1, 1), 1, Random(-3, 3), Random(-3, 3), Random(1, 3), 0, 0, 70)
			stop
	}
}

actor ExSmallBloodSplash 
{
	+NOBLOCKMAP
	+NOGRAVITY
	
	States 
	{
		Spawn:
			NULL A 2
			NULL AAAAAA 0 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 1, Random(-2, 2), Random(-2, 2), Random(1, 3), 0, 0, 70)
			stop
	}
}

actor ExBloodDrop : ExAbstractGoreChunk 
{
	Gravity 0.4
	Scale 0.3
	Mass 1

	+THRUACTORS

	States 
	{
		Fly:
			BDRP A 1
			loop

		Death:
			NULL A 0 A_JumpIf(CallACS("ExActivatorIsOnLava") == 1, "LavaSmoke")
			NULL A 0 A_SpawnItemEx("ExSmallBloodSplash")
			NULL A 0 A_Jump(256, "Splat1", "Splat2", "Splat3", "Splat4", "Splat5")
			stop
			
		Splat1:
			NULL A 0 A_SpawnItemEx("ExBloodDropModel1")
			stop
			
		Splat2:
			NULL A 0 A_SpawnItemEx("ExBloodDropModel2")
			stop
			
		Splat3:
			NULL A 0 A_SpawnItemEx("ExBloodDropModel3")
			stop
			
		Splat4:
			NULL A 0 A_SpawnItemEx("ExBloodDropModel4")
			stop
			
		Splat5:
			NULL A 0 A_SpawnItemEx("ExBloodDropModel5")
			stop
			
		LavaSmoke:
			NULL A 0 A_SpawnItemEx("ExSmokeSmall")
			NULL A 0 A_PlaySound("explayer/footsteps/lava", CHAN_AUTO, 0.2)
			stop	
	}
}

actor ExAbstractBloodDropModel 
{
	RenderStyle Shaded
	StencilColor "50 0a 0a"

	States
	{
		Spawn:
			MODE L 0 NoDelay A_SetAngle(Random(0, 359))
			MODE L 300 A_SetScale(fRandom(0.65, 1.0))
			MODE LLL 4 A_SetScale(scaleX - 0.1)
			stop
	}
}

actor ExBloodDropModel1 : ExAbstractBloodDropModel { }

actor ExBloodDropModel2 : ExAbstractBloodDropModel 
{
	StencilColor "7a 0b 0b"
}

actor ExBloodDropModel3 : ExAbstractBloodDropModel 
{
	StencilColor "8e 16 16"
}

actor ExBloodDropModel4 : ExAbstractBloodDropModel 
{
	StencilColor "89 09 09"
}

actor ExBloodDropModel5 : ExAbstractBloodDropModel 
{
	StencilColor "6b 08 08"
}

//spawned when actor is hit
actor ExBloodHit 
{
	Mass 5
	Scale 0.3

	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	-ALLOWPARTICLES
	+FORCEXYBILLBOARD

	States 
	{
		Spawn:
			BLDH A 1
			BLDH AAA 1 A_SpawnItemEx("ExBloodDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(-1, 1), 0, 0, 128)
			NULL AAA 0 A_SpawnItemEx("ExBloodDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(-1, 1), 0, 0, 128)
			BLDH B 4 A_Jump(64, "Vanish")
			BLDH C 4
			Stop
		
		Spray:
			BLDW ABCDE 16
			BLDW F 250
			BLDW FFFFFFFFFF 3 A_FadeOut(0.1)
			stop
		
		Vanish:
			BLDH A 4
			stop
	}
}

actor ExBloodSplatter 
{
	Radius 4
	Height 8
	Mass 2
	Scale 0.4

	+NOBLOCKMAP
	+MISSILE
	+DROPOFF
	+NOTELEPORT
	+CANNOTPUSH
	-ALLOWPARTICLES
	+FORCEXYBILLBOARD

	States 
	{
		Spawn:
			BLDH A 1
			BLDH AAA 1 A_SpawnItemEx("ExBloodDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(-1, 1), 0, 0, 128)
			NULL AAA 0 A_SpawnItemEx("ExBloodDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(-1, 1), 0, 0, 128)
			BLDH BCD 4
			BLDH E 4 A_Jump(64, "Vanish")
			BLDH F 4
			Stop

		Spray:
			BLDW ABCDE 16
			BLDW F 250
			BLDW FFFFFFFFFF 3 A_FadeOut(0.1)
			stop

		Death:
			BLDH BA 4
			stop
			
		Vanish:
			BLDH DCB 4
			stop	
	}
}

actor ExBloodParticle : ExAbstractChunk 
{
	Scale 0.1
	Gravity 0.8

	+FORCEXYBILLBOARD
	
	States 
	{
		Spawn:
			BLDC ABC 8
			stop
	}
}

//UNDERWATER
actor ExUnderwaterBloodParticle : ExBloodParticle 
{
	+NOGRAVITY
	
	States 
	{
		Spawn:
			BLDC A 8 NoDelay A_ChangeVelocity(0, 0, FRandom(0.1, 0.25))
			BLDC BC 8
			stop
	}
}

actor ExBloodCloud : ExBloodParticle 
{
	Scale 1.3
	RenderStyle Translucent
	Alpha 0.5
	Gravity 0.1
}

actor ExUnderwaterBloodCloud : ExBloodCloud 
{
	Scale 1.6
	
	+NOGRAVITY

	States 
	{
		Spawn:
			BLDC A 20 NoDelay A_ChangeVelocity(0, 0, FRandom(0.1, 0.3))
			BLDC BC 20
			stop
	}
}

actor ExUnderwaterBloodCloudSmall : ExUnderwaterBloodCloud 
{
	Scale 0.6
}

//GORE
actor ExGore1 : ExAbstractGoreChunk 
{
	Gravity 0.7

	States 
	{
		/*Spawn:
			GOR1 A 0 NoDelay A_SetScale(ScaleX * random(-1, 1), ScaleY)
			goto Fly*/
		
		Fly:
			GOR1 AAAABBBBCCCCDDDD 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
			
		Lay:
			GOR1 E 250
			NULL A 0 A_CheckSight("Vanish")
			loop
	}
}

actor ExGore2 : ExAbstractGoreChunk 
{
	Gravity 0.5
	
	States 
	{
		/*Spawn:
			GOR2 A 0 NoDelay A_SetScale(ScaleX * random(-1, 1), ScaleY)
			goto Fly*/
		
		Fly:
			GOR2 A 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
	}
}

actor ExGore3 : ExAbstractGoreChunk 
{
	Gravity 0.4

	States 
	{
		Fly:
			GOR3 A 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
	}
}

actor ExGore4 : ExAbstractGoreChunk 
{
	Gravity 0.65

	States 
	{
		Fly:
			GOR4 A 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
	}
}

actor ExGore5 : ExAbstractGoreChunk 
{
	States 
	{
		Fly:
			GOR5 A 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
	}
}

actor ExGoreGuts : ExAbstractGoreChunk 
{
	States 
	{
		Fly:
			GOR6 A 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
			
		Lay:
			GOR6 B 250
			NULL A 0 A_CheckSight("Vanish")
			loop
	}
}

actor ExGore7 : ExAbstractGoreChunk
{
	Gravity 0.9

	States 
	{
		Fly:
			GOR7 A 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
			
		Lay:
			GOR7 B 250
			NULL A 0 A_CheckSight("Vanish")
			loop
	}
}

actor ExGoreRibcage : ExAbstractGoreChunk 
{
	Gravity 0.95

	States 
	{
		Fly:
			GOR8 AAAABBBBCCCCDDDD 1 A_SpawnItemEx("ExBloodParticle", Random(-1, 1), Random(-1, 1), 0)
			loop
			
		Lay:
			GOR8 B 250
			NULL A 0 A_CheckSight("Vanish")
			loop
	}
}