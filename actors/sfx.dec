#region ============= BUBBLES

actor ExUnderwaterBubble 
{
	Scale 0.15
	RenderStyle Translucent
	Alpha 0.65

	+NOGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	+CANNOTPUSH
	+DONTSPLASH
	+FORCEXYBILLBOARD
	
	States 
	{
		Spawn:
			BUBL A 0 NoDelay A_SetScale(FRandom(0.05, 0.35))
			BUBL ABCDEFG 6 A_ChangeVelocity(0, 0, velz + FRandom(0.01, 0.02))
			goto Float

		Float:
			BUBL G 6 A_CheckCeiling("Vanish")
			NULL A 0 A_Jump(15, "Vanish")
			NULL A 0 A_JumpIf(waterlevel < 3, "VanishInstantly")
			NULL A 0 A_ChangeVelocity(FRandom(-0.2, 0.2), FRandom(-0.2, 0.2), 0)
			loop

		Vanish:
			BUBL G 1 A_SetScale(ScaleX - 0.02)
			BUBL G 0 A_JumpIf(ScaleX < 0, "VanishInstantly")
			loop
					 
		VanishInstantly:
			NULL A 0
			stop
	}
}

actor ExUnderwaterBloodBubble : ExUnderwaterBubble 
{
	States 
	{
		Spawn:
			BBBL A 0 NoDelay A_SetScale(FRandom(0.05, 0.35))
			BBBL ABCDEFG 6 A_ChangeVelocity(0, 0, velz + FRandom(0.004, 0.007))
			goto Float

		Float:
			BBBL G 6 A_CheckCeiling("Vanish")
			NULL A 0 A_Jump(15, "Vanish")
			NULL A 0 A_JumpIf(waterlevel < 3, "VanishInstantly")
			NULL A 0 A_ChangeVelocity(FRandom(-0.1, 0.1), FRandom(-0.1, 0.1), 0)
			loop
					 
		Vanish:
			BBBL G 1 A_SetScale(ScaleX - 0.02)
			BBBL G 0 A_JumpIf(ScaleX < 0, "VanishInstantly")
			loop

		VanishInstantly:
			NULL A 0
			stop
	}
}

actor ExInstantUnderwaterBubble : ExUnderwaterBubble
{
	States
	{
		Spawn:
			BUBL G 0 NoDelay A_SetScale(FRandom(0.05, 0.35))
			BUBL G 0 A_ChangeVelocity(0, 0, FRandom(1.0, 1.35))
			goto Float
	}
}

//given to player to spawn air bubbles when underwater
actor ExUwPlayerBubbleSpawner : CustomInventory 
{
	Inventory.MaxAmount 0
	
	States 
	{
		Pickup:
			NULL A 0 A_PlaySound("explayeruw/bubble", CHAN_AUTO | CHAN_UI, 0.25)
			NULL AAAAAAA Random(2,4) A_SpawnItemEx("ExUnderwaterBubble", Random(12, 16), Random(-6, 6), 46)
			stop
	}
}

//given when player is underwater
actor ExUnderwaterSFXSpawner : CustomInventory 
{
	Inventory.MaxAmount 0
	
	States
	{
		Pickup:
			NULL AAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("ExUnderwaterParticle", Random(-256, 256), Random(-256, 256), Random(-128, 128))
			stop
	}
}

actor ExUnderwaterParticle 
{
	Mass 10
	Alpha 0.0
	RenderStyle Add
	
	+NOTELEPORT
	+CANNOTPUSH
	+DONTSPLASH
	+FORCEXYBILLBOARD

	States 
	{
		Spawn:
			NULL A 0 NoDelay A_JumpIf(waterlevel != 3, "Vanish")
			NULL A 0 A_SetScale(FRandom(0.05, 0.1))
			UWPR TTTTTTT 1 A_FadeIn(0.05)
			UWPR T Random(16, 32)
			UWPR TTTTTTTTTTTTTT 1 A_FadeOut(0.025)
			stop

		Vanish:
			NULL A 0
			stop
	}
}

actor ExBubbleSpawner : ExAbstractSpawner 561 
{
	//$Category SFX
	//$Title Bubble Spawner
	//$Sprite BUBLC0

	States 
	{
		Spawn:
			NULL A Random(64, 128)
			NULL A 0 A_CheckSight("Spawn")
			NULL AAAAAA Random(2, 4) A_SpawnItemEx("ExUnderwaterBubble", Random(12, 16), Random(-6, 6), 46)
			loop
	}
}

#endregion

#region ============= FOAM

actor ExWaterFoam : ExUnderwaterBubble
{
	Alpha 0.0
	Scale 0.4
	RenderStyle Add
	
	States
	{
		Spawn:
			FOAM A 0 NoDelay A_SetScale(ScaleX + FRandom(-ScaleX / 3, ScaleX / 3))
			goto PickFrame
			
		PickFrame:
			FOAM A 0 A_Jump(256, 1, 2, 3, 4, 5, 6, 7, 8)
			FOAM ABCDEFGH 0 A_Jump(256, "GrowAndFadeIn")
			stop
				
		GrowAndFadeIn:
			"----" "#" 1 A_FadeIn(0.08, false)
			"----" "#" 0 A_SetScale(ScaleX + 0.01)
			"----" "#" 0 A_JumpIf(alpha > 0.7, "GrowAndVanish") 
			loop
				
		GrowAndVanish:
			"----" "#" 1 A_FadeOut(0.05)
			"----" "#" 0 A_SetScale(ScaleX + 0.01)
			loop
	}
}

actor ExWaterFoamSmall : ExWaterFoam
{
	Scale 0.2
	
	States
	{
		GrowAndFadeIn:
			"----" "#" 1 A_FadeIn(0.05, false)
			"----" "#" 0 A_SetScale(ScaleX + 0.01)
			"----" "#" 0 A_JumpIf(alpha > 0.6, "GrowAndVanish") 
			loop
	}
}

actor ExWaterFoamBig : ExWaterFoam
{
	Scale 0.6
	
	States
	{
		GrowAndFadeIn:
			"----" "#" 1 A_FadeIn(0.1, false)
			"----" "#" 0 A_SetScale(ScaleX + 0.01)
			"----" "#" 0 A_JumpIf(alpha > 0.8, "GrowAndVanish") 
			loop
	}
}

actor ExWaterFoamSpawner : ExAbstractSpawner 5608 
{
	//$Category SFX
	//$Title Foam Spawner
	//$Sprite FOAMA0
	//$arg0 Width
	//$arg1 Strength (0-2)
	//$arg2 Density (0-2)
	//$arg3 Acceleration
	
	Scale 0.3

	States 
	{
		Spawn:
			NULL A 1 NoDelay A_SetArg(0, args[0] / 2)
			NULL A 0 A_JumpIf(args[1] == 0, "LightStrength")
			NULL A 0 A_JumpIf(args[1] == 1, "MediumStrength")
			NULL A 0 A_JumpIf(args[1] == 2, "HeavyStrength")
			stop
			
		LightStrength:
			NULL AAAA 1 A_SpawnItemEx("ExWaterFoamSmall", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			NULL A 0 A_JumpIf(args[2] == 0, "LightStrength")
			NULL AAAA 0 A_SpawnItemEx("ExWaterFoamSmall", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			NULL A 0 A_JumpIf(args[2] == 1, "LightStrength")
			NULL AAAA 0 A_SpawnItemEx("ExWaterFoamSmall", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			loop	
			
		MediumStrength:
			NULL AAAAAA 1 A_SpawnItemEx("ExWaterFoam", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			NULL A 0 A_JumpIf(args[2] == 0, "MediumStrength")
			NULL AAAAAA 0 A_SpawnItemEx("ExWaterFoam", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			NULL A 0 A_JumpIf(args[2] == 1, "MediumStrength")
			NULL AAAAAA 0 A_SpawnItemEx("ExWaterFoam", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			loop
		
		HeavyStrength:
			NULL AAAAAAAA 1 A_SpawnItemEx("ExWaterFoamBig", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			NULL A 0 A_JumpIf(args[2] == 0, "HeavyStrength")
			NULL AAAAAAAA 0 A_SpawnItemEx("ExWaterFoamBig", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			NULL A 0 A_JumpIf(args[2] == 1, "HeavyStrength")
			NULL AAAAAAAA 0 A_SpawnItemEx("ExWaterFoamBig", 0, Random(-args[0], args[0]), height / 2, args[3], 0, 0)
			loop	
	}
}

#endregion

#region ============= PUFFS

actor ExBulletPuff : BulletPuff
{
	RenderStyle Translucent
	Alpha 0.6
	Scale 0.4
	Decal "ExBulletHole"
  
	SeeSound "bullet/hit"
	AttackSound "bullet/ricochete"

	+NOBLOCKMAP
	+NOGRAVITY
	+NOEXTREMEDEATH
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+PUFFONACTORS

	States 
	{
		Crash:
			NULL AAAAAAAAAAAAAA 0 A_CustomMissile("ExBulletSparkle", 0, 0, Random(-90, 90), CMF_AIMDIRECTION, Random(-45, 45))
			BPUF ABCDEF 2
			stop
	}
}

actor ExMachetePuff : BulletPuff
{
	RenderStyle Translucent
	Alpha 0.6
	Scale 0.4
	
	SeeSound "machete/hit"
	AttackSound "machete/hitwall"
	ActiveSound "machete/swing"
	
	+NOBLOCKMAP
	+NOGRAVITY
	+NOEXTREMEDEATH
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+PUFFONACTORS
	
	States 
	{
		Crash:
			NULL A 0
			NULL AAAAAAAAAAAAAA 0 A_CustomMissile("ExBulletSparkle", 0, 0, Random(-90, 90), CMF_AIMDIRECTION, Random(-45, 45))
			BPUF ABCDEF 2
			stop	
	}
}

//SPARKLES
actor ExBulletSparkle : ExAbstractParticle 
{
	Scale 0.03
	Gravity 0.1
	Speed 2
	
	States 
	{
		Spawn:
			NULL A 0
			goto Fly

		Fly:
			BCTR A 1 Bright A_SetScale(ScaleX - 0.002)
			BCTR A 0 A_JumpIf(ScaleX < 0, "Vanish")
			BCTR A 0 A_SpawnItemEx("ExBulletSparkleTrail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERALPHA | SXF_TRANSFERRENDERSTYLE)
			loop
	}
}

actor ExBulletSparkleTrail 
{
	Scale 0.03

	+NOGRAVITY
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+NOBOUNCESOUND
	+DONTSPLASH

	States 
	{
		Spawn:
			BCTR A 1 NoDelay Bright A_SetScale(ScaleX - 0.007)
			BCTR A 0 A_JumpIf(ScaleX < 0, "Vanish")
			loop

		Vanish:
			NULL A 0
			stop
	}
}

actor ExFireEmber : ExAbstractParticle
{
	Scale 0.05
	Alpha 0.1
	
  +NOGRAVITY
	+NOBLOCKMAP
	
	States 
	{
		Spawn:
			NULL A 0 NoDelay A_ChangeVelocity(FRandom(-0.1, 0.1), FRandom(-0.1, 0.1), FRandom(1.0, 1.3))
			goto Fly

		Fly:
			BCTR A 1 Bright A_SetScale(ScaleX - 0.001)
			BCTR A 0 A_JumpIf(ScaleX < 0, "Vanish")
			BCTR A 0 A_FadeIn(0.05)
			loop
	}
}

#endregion 

#region ============= EXPLOSIONS

actor ExGrenadeExplosion 
{
	RenderStyle "None"
	
	+NOBLOCKMAP
	+NOGRAVITY

	States 
	{
		Spawn:
			NXPL A 0 NoDelay A_JumpIf(waterlevel == 3, "UnderwaterExplosion")
			NXPL AAAAAAAAAA 0 A_SpawnItemEx("ExSmokeDark", Random(10, 70) * RandomPick(-1, 1), Random(10, 70) * RandomPick(-1, 1), 6, 0, 0, 0.1, 0, SXF_NOCHECKPOSITION)
			NXPL AAAAAAAAAAAAA 0 A_SpawnItemEx("ExFireCracker", Random(-20, 20), Random(-20, 20), Random(6, 40), 
																													Random(1, 3) * RandomPick(-1, 1), Random(1, 3) * RandomPick(-1, 1), Random(6, 8), 
																													0, SXF_NOCHECKPOSITION)
			NXPL A 0 A_SpawnItemEx("ExExplosionTop", 0, 0, 60, 0, 0, 2, 0, SXF_NOCHECKPOSITION)
			NXPL A 0 A_SpawnItemEx("ExSmokeDarkBigLowAlpha", 0, 0, 40, 0, 0, 0.1, 0, SXF_NOCHECKPOSITION)
			NXPL A 26 A_QuakeEx(1, 1, 1, 26, 0, 450, "", QF_SCALEDOWN | QF_WAVE) //A_QuakeEx works only while the actor exists in a map
			stop
			
		UnderwaterExplosion:
			NXPL AAAAAAAAAA 0 A_SpawnItemEx("ExSmokeDark", Random(10, 70) * RandomPick(-1, 1), Random(10, 70) * RandomPick(-1, 1), 6, 0, 0, 0.1, 0, SXF_NOCHECKPOSITION)
			NXPL AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ExInstantUnderwaterBubble", Random(-40, 40), Random(-40, 40), Random(0, 30),	0, 0, 0, 0, SXF_NOCHECKPOSITION)
			NULL AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ExUnderwaterParticle", Random(-128, 128), Random(-128, 128), Random(0, 64))
			NXPL A 0 A_SpawnItemEx("ExSmokeDarkBigLowAlpha", 0, 0, 40, 0, 0, 0.1, 0, SXF_NOCHECKPOSITION)
			NXPL A 26 A_QuakeEx(1, 1, 1, 26, 0, 450, "", QF_SCALEDOWN | QF_WAVE) //A_QuakeEx works only while the actor exists in a map
			stop	
	}
}

actor ExExplosionTop 
{
	RenderStyle Translucent
	XScale 1.3
	YScale 0.7

	+NOBLOCKMAP
	+NOGRAVITY
	+FORCEXYBILLBOARD
		
	States 
	{
		Spawn:
			NXPL ABCDEFGHIJKLMNOPQRST 2 Bright
			NXPL UVWXYZ 2 Bright A_FadeOut
			NXPM ABCDEF 2 Bright A_FadeOut
			stop
	}
}

actor ExFireCracker : ExAbstractParticle
{
	Scale 0.03
	Alpha 0.3
	Speed 1
	
	States
	{
		Spawn:
			//boy I could use some of them grouped action specials right about now...
			NXPL C 1 Bright A_SpawnItemEx("ExFireCrackerTrail", 0, 0, 0, 0, 0, 0.1, 0, SXF_TRANSFERALPHA)
			NULL A 0 A_FadeOut(0.005)
			NXPL D 1 Bright A_SpawnItemEx("ExFireCrackerTrail", 0, 0, 0, 0, 0, 0.1, 0, SXF_TRANSFERALPHA)
			NULL A 0 A_FadeOut(0.005)
			NXPL E 1 Bright A_SpawnItemEx("ExFireCrackerTrail", 0, 0, 0, 0, 0, 0.1, 0, SXF_TRANSFERALPHA)
			NULL A 0 A_FadeOut(0.005)
			NXPL D 1 Bright A_SpawnItemEx("ExFireCrackerTrail", 0, 0, 0, 0, 0, 0.1, 0, SXF_TRANSFERALPHA)
			NULL A 0 A_FadeOut(0.005)
			loop
	}
}

#endregion

#region ============= SMOKE

actor ExSmoke
{
	RenderStyle Translucent
	Scale 1.0
	Alpha 0.7

	+NOGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	+CANNOTPUSH
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	
	var float user_roll;
	
	States
	{
		Spawn:
			NSMK A 0 NoDelay
			goto Setup
		
		Setup:
			"####" "A" 0
			{
				A_SetScale(ScaleX + FRandom(-ScaleX * 0.2, ScaleX * 0.2));
				A_FadeOut(FRandom(-0.1, 0.05));
				A_ChangeVelocity(0, 0, FRandom(0.1, 0.2));
				user_roll = FRandom(0.5, 1.0) * randompick(-1, 1);
				return state("Float");
			}
		
		Float:
			"####" "A" 1
			{
				A_FadeOut(0.006);
				A_SetScale(ScaleX + 0.005);
				A_SetRoll(roll + user_roll, SPF_INTERPOLATE);
			}
			loop
	}
}

actor ExSmokeFastFade : ExSmoke
{
	States
	{
		Float:
			"####" "A" 1
			{
				A_FadeOut(0.03);
				A_SetScale(ScaleX + 0.03);
				A_SetRoll(roll + user_roll, SPF_INTERPOLATE);
			}
			loop
	}
}

actor ExSmokeLowAlpha : ExSmoke
{
	Alpha 0.5
	Scale 1.2
}

actor ExSmokeSmall : ExSmoke 
{
	Scale 0.3
	Alpha 0.9
}

actor ExSmokeSmallFastFade : ExSmokeFastFade 
{
	Scale 0.3
	Alpha 0.9
}

actor ExSmokeDark : ExSmoke
{
	//RenderStyle "Stencil"
	//StencilColor "28 25 23"//"11 11 11"
	States
	{
		Spawn:
			NSMD A 0
			goto Setup
	}
}

actor ExSmokeDarkSmall : ExSmokeDark
{
 	Scale 0.3
	Alpha 0.9
}

actor ExFireCrackerTrail : ExSmokeDark
{
	Scale 0.3
}

actor ExSmokeDarkBigLowAlpha : ExSmokeDark
{
	Alpha 0.4
	Scale 3.5
}

actor ExFlameSmall
{
	RenderStyle Translucent
	Alpha 0.8
	Scale 0.3
	
	+NOBLOCKMAP
	+NOGRAVITY
	+FORCEXYBILLBOARD
	
	States
	{
		Spawn:
			FMSM A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.1), ScaleY + frandom(-0.05, 0.05))
			goto Idle
			
		Idle:
			FMSM ABCD 3 Bright A_Jump(16, "Disappear")
			loop
			
		Disappear:
			FMSM A 3 Bright A_SetScale(ScaleX - 0.05, ScaleY - 0.05)
			FMSM B 3 Bright A_JumpIf(ScaleX < 0 || ScaleY < 0, "Vanish")
			FMSM C 3 Bright A_SetScale(ScaleX - 0.05, ScaleY - 0.05)
			FMSM D 3 Bright A_JumpIf(ScaleX < 0 || ScaleY < 0, "Vanish")
			loop
			
		Vanish:
			NULL A 0
			stop
	}
}

#endregion

#region ============= AMBIANCE

actor ExAmbientSound : SwitchableDecoration 999
{
	RenderStyle None

	+NOBLOCKMAP
	+NOGRAVITY
	
	States
	{
		Spawn:
			AMBS N 0
			goto Activate
			
		Activate:
			//If only there was a more elegant way to do this...  
			AMBS N 0 A_JumpIf(args[0] == 0, "NoSound")
			AMBS N 0 A_JumpIf(args[0] == 1, "wind1")
			AMBS N 0 A_JumpIf(args[0] == 2, "waterdrips1") 
			AMBS N 0 A_JumpIf(args[0] == 3, "lava1")
			AMBS N 0 A_JumpIf(args[0] == 4, "cave1")
			AMBS N 0 A_JumpIf(args[0] == 5, "chains1")
			AMBS N 0 A_JumpIf(args[0] == 6, "tomb1")
			stop
			
		Deactivate:
			AMBS N -1 A_StopSound(CHAN_BODY)
			stop	
			
		wind1:
			AMBS N -1 A_PlaySound("newambiance/wind1", CHAN_BODY, (100 - args[1]) / 100.0, true, args[2] / 10.0)
			stop
			
		waterdrips1:
			AMBS N -1 A_PlaySound("newambiance/waterdrips1", CHAN_BODY, (100 - args[1]) / 100.0, true, args[2] / 10.0)
			stop
			
		lava1:
			AMBS N -1 A_PlaySound("newambiance/lava1", CHAN_BODY, (100 - args[1]) / 100.0, true, args[2] / 10.0)
			stop
			
		cave1:
			AMBS N -1 A_PlaySound("newambiance/cave1", CHAN_BODY, (100 - args[1]) / 100.0, true, args[2] / 10.0)
			stop
			
		chains1:
			AMBS N -1 A_PlaySound("newambiance/chains", CHAN_BODY, (100 - args[1]) / 100.0, true, args[2] / 10.0)
			stop
			
		tomb1:
			AMBS N -1 A_PlaySound("newambiance/tomb1", CHAN_BODY, (100 - args[1]) / 100.0, true, args[2] / 10.0)
			stop				
		
		NoSound:
			AMBS N 0 A_Log("One of ExAmbientSound actors has no sound to play!")
			stop
	}
}

#endregion

#region ============= MISC

actor ExAbstractTrap : SwitchableDecoration
{
	Radius 8
	Height 16
	Activation THINGSPEC_Switch
	RenderStyle "None"
	
	+NOINTERACTION
	+NOBLOCKMAP
	+NOGRAVITY

	States
	{
		Spawn:
			TRAP A -1
			stop
			
		Inactive:
			TRAP A -1
			stop	
	}
}

#endregion