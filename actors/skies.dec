//SKYBOXES
actor ExSkyDome 5602 
{
	//$Category Skies
	//$Title Skydome (day)
	
	Radius 64
	Height 64
	
	States
	{
		Spawn:
			SKY1 A -1
			stop
	}
}

actor ExSkyDome2 5605 
{
	//$Category Skies
	//$Title Skydome (midday)
	
	Radius 64
	Height 64
	
	States
	{
		Spawn:
			SKY1 A -1
			stop
	}
}

actor ExSkyDomeClouds 5603 
{
	//$Category Skies
	//$Title Skydome clouds (day)
	
	Radius 60
	Height 60
	RenderStyle Shaded
	StencilColor "CC CC CC"

	States 
	{
		Spawn:
			SKY1 A 1 A_SetAngle(angle + 0.02)
			loop
	}
}

actor ExSkyDomeCloudsBG 5604 
{
	//$Category Skies
	//$Title Skydome clouds BG (day)
	
	Radius 62
	Height 62
	RenderStyle Shaded
	StencilColor "EE EE EE"
	Alpha 0.5
	
	States 
	{
		Spawn:
			SKY1 A 1 A_SetAngle(angle + 0.01)
			loop
	}
}

//SKYBOX ACTORS
actor ExBgTerrain1 5650 
{
	//$Category Skies
	//$Title Skydome Terrain 1
	
	Radius 45
	Height 16
	+NOGRAVITY
	
	States 
	{
		Spawn:
			SKY1 A -1
			stop
	}
}

//EFFECTS
actor ExSkyBird //args[1] == speed * 1000
{ 
	//Alpha 0.0
	Radius 1
	Height 1

	Projectile

	States 
	{
		Spawn:
			SBRD A 1
			SBRD A 1 A_SetScale(fRandom(0.005, 0.01))
			NULL A 0 A_SetArg(1, Random(15, 25)) //speed * 1000
			NULL A 0 A_ChangeVelocity(args[1] / 1000.0, 0, 0, CVF_REPLACE | CVF_RELATIVE)
			//SBRD AAAAAAAAAA 1 A_FadeIn(0.1)
			goto Glide

		Glide:
			SBRD A 64
			NULL A 0 A_Jump(32, "ChangeCourse")
			NULL A 0 A_Jump(32, "Circle")
			NULL A 0 A_Jump(196, "Glide")
			SBRD BCDEFGHI 4
			goto Flap
	  
		Flap:
			SBRD JKLMNOP 2
			SBRD QRSTSRQ 2
			SBRD PONMLKJ 2
			NULL A 0 A_Jump(196, "Flap")
			SBRD IHGFEDCB 3
			goto Glide
			
		ChangeCourse:
			NULL A 0 A_SetAngle(angle + Random(-3, 3))
			NULL A 0 A_ChangeVelocity (args[1] / 1000.0, 0, 0, CVF_REPLACE | CVF_RELATIVE)
			goto Glide

		Circle:
			NULL A 0 A_SetArg(0, Random(-2, 2))
			goto CircleLoop

		CircleLoop:
			SBRD A 1 A_SetAngle(angle + args[0])
			NULL A 0 A_ChangeVelocity(args[1] / 1000.0, 0, 0, CVF_REPLACE | CVF_RELATIVE)
			NULL A 0 A_Jump(3, "Flap")
			loop
			
		Death:
			NULL A 0
			stop
	}
}

actor ExSkyBirdsSpawner : ExAbstractSpawner 5601 
{
	//$Category Skies
	//$Title Birds spawner
	
	Radius 32
	Height 16
	
	States 
	{
		Spawn:
			FFLY A 64
			FFLY A 0 A_Jump(32, "SpawnBird")
			loop

		SpawnBird:
			NULL A 0 A_SetArg(0, Random(1, 360))
			NULL A 1 A_SpawnItemEx("ExSkyBird", sin(args[0]) * 32, cos(args[0]) * 32, Random(1, 16), 0, 0, 0, args[0] + Random(-10, 10), 0, 0)
			goto Spawn
	}
}